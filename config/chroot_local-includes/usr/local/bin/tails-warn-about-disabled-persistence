#! /usr/bin/perl

use strict;
use warnings FATAL => 'all';
use 5.10.1;
use autodie qw{:all};
use Desktop::Notify;
use Locale::gettext;
use POSIX;
use Path::Class;
use String::Errf qw{errf};

setlocale(LC_MESSAGES, "");
textdomain("tails");


=head1 Functions

=head2 current_lang

Returns the two-letters language code of the current session.

=cut
sub current_lang {
    my ($code) = ($ENV{LANG} =~ m/([a-z]{2}).*/);

    return $code;
}

=head2 doc_url

Returns the best local URL to a given piece of doc.

=cut
sub doc_url {
    my $website_root = shift;
    my $doc_resource = shift;

    my @try_files = (
        file($website_root, $doc_resource . "." . current_lang() . ".html" ),
        file($website_root, $doc_resource . ".en.html" ),
    );

    foreach my $file (@try_files) {
        return "file://$file" if -e $file;
    }

    return;
}

sub notify {
    my $summary = shift;
    my $body    = shift;

    my $notify = Desktop::Notify->new();
    $notify->create(
        summary => $summary,
        body    => $body,
        timeout => 0
    )->show();
}


=head1 Main

=cut

my @files_disabled_by_0_21_upgrade = (
    glob('/live/persistence/*_unlocked/live-additional-software.conf.disabled'),
    glob('/live/persistence/*_unlocked/live-persistence.conf.old'),
);

my @files_never_migrated = (
    glob('/live/persistence/*_unlocked/live-persistence.conf'),
);

my @files_disabled_for_wrong_access_rights = (
    glob('/live/persistence/*_unlocked/persistence.conf.insecure_disabled'),
    glob('/live/persistence/*_unlocked/live-additional-software.conf.insecure_disabled'),
);

my $website_root = '/usr/share/doc/tails/website';

my $doc_upgrade_resource = 'doc/first_steps/persistence/upgrade';
my $doc_upgrade_url = doc_url($website_root, $doc_upgrade_resource)
    or die "Could not find best URL for '$doc_upgrade_resource' at '$website_root'";

my $doc_insecure_disabled_resource = 'doc/first_steps/persistence/insecure_disabled';
my $doc_insecure_disabled_url = doc_url($website_root, $doc_insecure_disabled_resource)
    or die "Could not find best URL for '$doc_insecure_disabled_resource' at '$website_root'";

my @notifications;

if (@files_disabled_by_0_21_upgrade + @files_never_migrated) {
    push @notifications, {
        disabled_conf_files => join(
            ', ',
            @files_disabled_by_0_21_upgrade,
            @files_never_migrated
        ),
        doc_url             => $doc_upgrade_url,
    };
}

if (@files_disabled_for_wrong_access_rights) {
    push @notifications, {
        disabled_conf_files => join(', ', @files_disabled_for_wrong_access_rights),
        doc_url             => $doc_insecure_disabled_url,
    };
}

foreach my $notification_params (@notifications) {
    notify(
        gettext(q{Some persistence settings were temporarily disabled}),
        errf(
            gettext(
                "%{disabled_conf_files}s\n"
                . "<a href='%{doc_url}s'>Learn how to enable them again.</a>"
            ),
            $notification_params,
        )
    );
}
